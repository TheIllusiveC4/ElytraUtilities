plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

archivesBaseName = "${mod_id}-fabric"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${fabric_parchment_mc_version}:${fabric_parchment_version}@zip")
    })
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    // Optional
    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from("../CHANGELOG.md")
    from("../LICENSE")
    from("../README.md")
    from project(":common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

sourceSets.main.resources.srcDir '../.gpl'

tasks.withType(JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

curseforge {

    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${fabric_curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'text'
        changelog = "Changelog: https://github.com/TheIllusiveC4/ElytraUtilities/CHANGELOG#{$changelog}"
        addGameVersion "Java 17"
        addGameVersion "Fabric"
        addArtifact(sourcesJar)
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
        "${curse_versions}".split(',').each {
            addGameVersion "${it}"
        }
        relations {
            requiredDependency 'fabric-api'
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
